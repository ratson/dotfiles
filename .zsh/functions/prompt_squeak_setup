# -*- mode: shell-script -*-
# vi: set ft=zsh :

function prompt_squeak_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c${_prompt_steeef_colors[4]}â—%f)"
  else
    branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  # Ignore `git config oh-my-zsh.hide-status 1`
  if [[ "$(command git config --get oh-my-zsh.hide-status 2>/dev/null)" != "1" ]]; then
    vcs_info 'prompt'
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi

  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_squeak_setup {
  zstyle ':prezto:module:node:info:version' format 'node:%v'

  prompt_steeef_setup "$@"
  add-zsh-hook -d precmd prompt_steeef_precmd
  add-zsh-hook precmd prompt_squeak_precmd

  RPROMPT='$node_info[version]${NAVE+,}$NAVE'
}

prompt_squeak_setup "$@"
